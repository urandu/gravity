## <summary>policy for gravity</summary>

########################################
## <summary>
##	Execute gravity in the gravity domain and allow
## the specified role access to the gravity domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
## <param name="role">
## <summary>
## 	Role allowed access.
## </summary>
## </param>
#
interface(`gravity_run',`
  gen_require(`
    attribute_role gravity_roles;
  ')

  gravity_domtrans($1)
  roleattribute $2 gravity_roles;
')

########################################
## <summary>
##	Enable user role access to gravity domain
## </summary>
## <param name="role">
## <summary>
##	User role to allow access to gravity domain.
## </summary>
## </param>
## <param name="domain">
## <summary>
##	User domain to allow access to gravity domain.
## </summary>
## </param>
#
interface(`gravity_role',`
  gen_require(`
    attribute_role gravity_roles;
    attribute gravity_domain;
    type gravity_t;
    type gravity_exec_t;
    type gravity_container_runtime_t;
    type gravity_container_runtime_exec_t;
    type gravity_home_t;
    type gravity_install_home_t;
    type gravity_log_t;
    type gravity_unit_file_t;
  ')

  # Allow gravity domain to use the user role
  roleattribute $1 gravity_roles;

  # Allow domain transition for user domain to gravity/planet
  domtrans_pattern($2, gravity_exec_t, gravity_t)
  domtrans_pattern($2, gravity_container_runtime_exec_t, gravity_container_runtime_t)

  # Interact with gravity process
  ps_process_pattern($2, gravity_domain)
  allow $2 gravity_domain:process { ptrace signal_perms };

  admin_pattern($2, gravity_home_t)
  admin_pattern($2, gravity_install_home_t)
  admin_pattern($2, gravity_log_t)
  allow $2 gravity_unit_file_t:service manage_service_perms;
')

########################################
## <summary>
##	Execute gravity_exec_t in the specified domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`gravity_domtrans',`
  gen_require(`
    type gravity_t, gravity_exec_t;
  ')

  corecmd_search_bin($1)
  domtrans_pattern($1, gravity_exec_t, gravity_t)
')

######################################
## <summary>
##	Execute gravity in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`gravity_exec',`
  gen_require(`
    type gravity_exec_t;
  ')

  corecmd_search_bin($1)
  can_exec($1, gravity_exec_t)
')

########################################
## <summary>
##	Search gravity state directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`gravity_search_state',`
  gen_require(`
    type gravity_home_t;
  ')

  allow $1 gravity_home_t:dir search_dir_perms;
  files_search_var_lib($1)
')

########################################
## <summary>
##	Read gravity state files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`gravity_read_state_files',`
  gen_require(`
    type gravity_home_t;
  ')

  files_search_var_lib($1)
  read_files_pattern($1, gravity_home_t, gravity_home_t)
  read_lnk_files_pattern($1, gravity_home_t, gravity_home_t)
')

########################################
## <summary>
##	Manage gravity state files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`gravity_manage_state_files',`
  gen_require(`
    type gravity_home_t;
  ')

  files_search_var_lib($1)
  manage_files_pattern($1, gravity_home_t, gravity_home_t)
  manage_lnk_files_pattern($1, gravity_home_t, gravity_home_t)
')

########################################
## <summary>
##	Manage gravity state directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`gravity_manage_state_dirs',`
  gen_require(`
    type gravity_home_t;
  ')

  files_search_var_lib($1)
  manage_dirs_pattern($1, gravity_home_t, gravity_home_t)
')

########################################
## <summary>
##	Read gravity container state files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`gravity_read_container_files',`
  gen_require(`
    type gravity_container_home_t;
  ')

  files_search_var_lib($1)
  read_files_pattern($1, gravity_container_home_t, gravity_container_home_t)
  read_lnk_files_pattern($1, gravity_container_home_t, gravity_container_home_t)
')



########################################
## <summary>
##	All of the rules required to administrate
##	a gravity environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`gravity_admin',`
  gen_require(`
    type gravity_t;
    type gravity_home_t;
  ')

  allow $1 gravity_t:process { signal_perms };
  ps_process_pattern($1, gravity_t)

  tunable_policy(`deny_ptrace',`',`
    allow $1 gravity_t:process ptrace;
  ')

  files_search_var_lib($1)
  admin_pattern($1, gravity_home_t)
  optional_policy(`
    systemd_passwd_agent_exec($1)
    systemd_read_fifo_file_passwd_run($1)
  ')
')

########################################
## <summary>
##	Execute planet in the planet domain and allow
## the specified role access to the planet domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
## <param name="role">
## <summary>
## 	Role allowed access.
## </summary>
## </param>
#
interface(`gravity_container_runtime_run',`
  gen_require(`
    attribute_role gravity_container_runtime_roles;
  ')

  gravity_container_runtime_domtrans($1)
  roleattribute $2 gravity_container_runtime_roles;
')

########################################
## <summary>
##	Execute planet in the specified domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`gravity_container_runtime_domtrans',`
  gen_require(`
    type gravity_container_runtime_t, gravity_container_runtime_exec_t;
  ')

  corecmd_search_bin($1)
  domtrans_pattern($1, gravity_container_runtime_exec_t, gravity_container_runtime_t)
  allow gravity_container_runtime_t $1:fifo_file setattr;
')

interface(`gravity_container_runtime_exec',`
  gen_require(`
    type gravity_container_runtime_exec_t;
  ')

  corecmd_search_bin($1)
  can_exec($1, gravity_container_runtime_exec_t)
')

interface(`gravity_container_stream_connect',`
  gen_require(`
    attribute gravity_domain;
    type gravity_container_var_run_t;
  ')

  files_search_pids($1)
  stream_connect_pattern($1, gravity_container_var_run_t, gravity_container_var_run_t, gravity_domain)
')
